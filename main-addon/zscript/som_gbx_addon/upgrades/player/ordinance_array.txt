class SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo : object play
{
	Ammo ammo_class;
	String ammo_id;
	String ammo_name;
	uint ammo_max_amt;
	uint ammo_curr_amt;
	uint res_max_amt;
	uint res_curr_amt;
	
	void init(Actor owner, string _id, uint level)
	{
		ammo_id = _id;
		
		update_data(owner, level);
		
		class<Actor> cls = _id;
		
		ammo_name = Actor(GetDefaultByType(cls)).GetTag();
		
		if (ammo_name == "")
		{
			ammo_name = _id;
		}
		
		//Console.Printf("Ammo Type '%s' added!", ammo_name);
	}
	
	void update_data(Actor owner, uint level)
	{
		Let p = owner;
		
		if (!ammo_class)
		{
			ammo_class = Ammo(p.FindInventory(ammo_id));
			
			if (!ammo_class) { Return; }
		}
		
		int _max = ammo_class.MaxAmount;
		int _bp = ammo_class.BackpackMaxAmount;
		
		ammo_curr_amt = p.CountInv(ammo_id);
		
		if (_max > _bp)
		{
			ammo_max_amt = ammo_class.MaxAmount;
		}
		else
		{
			ammo_max_amt = (p.CountInv("Backpack")) ? _bp : _max;
		}
		
		res_max_amt = round(ammo_max_amt * (som_gbx_upgr_ordinance_array_inc * level));
	}
	
	int modify_reserve(int amt)
	{
		int new = res_curr_amt + amt;
		
		if (new > res_max_amt)
		{
			amt = res_max_amt - res_curr_amt;
		}
		
		if (new < 0)
		{
			amt = -res_curr_amt;
		}
		
		res_curr_amt += amt;
		
		return (abs(amt));
	}
}

class SOM_GBX_Upgrade_OrdinanceArray : TFLV_Upgrade_BaseUpgrade
{
	array<SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo> ammo_info;
	
	SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo GetInfoFor(string _id) const
	{
		for (int i = 0; i < ammo_info.size(); ++i)
		{
			if (ammo_info[i].ammo_id == _id)
			{
				return ammo_info[i];
			}
		}
		
		return null;
	}
	
	bool is_valid_ammo(Ammo ammo)
	{
		bool valid = (GetDefaultByType(ammo.GetClass()).BackpackAmount != 0) || (GetDefaultByType(ammo.GetClass()).FindState("Spawn").Sprite != 0);
		//GetDefaultByType(ammo.GetClass()).FindState("Spawn").Sprite != 0;
		
		return (valid);
	}
	
	void find_new_ammo(Actor owner)
	{
		Let p = owner;
		
		for (Inventory inv = p.inv; inv != null; inv = inv.inv)
		{
			let ammo = Ammo(inv);
			
			if (!ammo) continue;
			
			string _id = ammo.GetClassName();
			
			Let _info = GetInfoFor(_id);
			
			if (!_info)
			{
				if (!is_valid_ammo(ammo))
				{
					continue;
				}
			
				_info = new("SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo");
				_info.init(owner, _id, level);
				ammo_info.push(_info);
			}
		}
	}
	
	override bool IsSuitableForPlayer(TFLV_PerPlayerStats stats)
	{
		return (som_gbx_upgr_ordinance_array_enabled);
	}
	
	override void Tick(Actor owner)
	{
		string evt = "som-gbext-amres-display-info";
		
		if (enabled)
		{
			Let p = owner;
			
			find_new_ammo(owner);
			
			SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo info;
			int ammo = 0;
			int hi = 0;
			//int lo = 0;
			int amt = 0;
			
			for (int i = 0; i < ammo_info.Size(); i++)
			{
				info = ammo_info[i];
				
				info.update_data(owner, level);
				
				ammo = p.CountInv(info.ammo_id);
				
				hi = round(info.ammo_max_amt * som_gbx_upgr_ordinance_array_si_per);
				//lo = round(info.ammo_max_amt * som_gbx_upgr_ordinance_array_re_per);
				
				/*if (lo >= hi)
				{ 
					continue;
				}*/
				
				if (ammo > hi)
				{
					amt = ammo - hi;
					
					amt = info.modify_reserve(amt);
					
					p.TakeInventory(info.ammo_id, amt);
					
					if (amt > 0)
					{
						//Console.Printf("Added %d %s to reserve (%d/%d).", amt, info.ammo_name, info.res_curr_amt, info.res_max_amt);
					}
				}
				else
				{
					if ((info.res_curr_amt > 0) && (ammo < hi))
					{
						amt = hi - ammo;
						
						if (amt > 0)
						{
							amt = info.modify_reserve(-amt);
							p.GiveInventory(info.ammo_id, amt);

							if (amt > 0)
							{
								//Console.Printf("Took %d %s from reserve (%d/%d).", amt, info.ammo_name, info.res_curr_amt, info.res_max_amt);
							}
						}
					}
				}
			}
			
			// Send info (gross...)
			let pp = players[consoleplayer];
			let weap = pp.readyweapon;
			
			if (weap)
			{
				Let ammo = pp.readyweapon.ammotype1;
				
				if (ammo)
				{
					info = GetInfoFor(ammo.GetClassName());
					
					if (info)
					{
						evt = String.Format("%s:%s:%d:%d", evt, info.ammo_name, info.res_curr_amt, info.res_max_amt);
					}
				}

				ammo = pp.readyweapon.ammotype2;
				
				if (ammo)
				{
					info = GetInfoFor(ammo.GetClassName());
					
					if (info)
					{
						evt = String.Format("%s:%s:%d:%d", evt, info.ammo_name, info.res_curr_amt, info.res_max_amt);
					}
				}
			}
		}
		
		EventHandler.SendNetworkEvent(evt);
	}
	
	override void OnActivate(TFLV_PerPlayerStats stats, TFLV_WeaponInfo info)
	{
		super.OnActivate(stats, info);
	}

	override void OnDeactivate(TFLV_PerPlayerStats stats, TFLV_WeaponInfo info)
	{
		super.OnDeactivate(stats, info);
		
		EventHandler.SendNetworkEvent("som-gbext-amres-display-info");
	}

	override void GetTooltipFields(Dictionary fields, uint level)
	{
		fields.insert("ammo-pool", AsPercent((som_gbx_upgr_ordinance_array_inc * level)));
	}
}
