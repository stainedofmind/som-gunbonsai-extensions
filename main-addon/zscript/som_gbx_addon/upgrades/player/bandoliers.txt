class SOM_GBX_Upgrade_Bandoliers: TFLV_Upgrade_BaseUpgrade
{
	float AmmoBonus(uint level)
	{
		return 1.0 + 0.5 * level;
	}

	void UpdateAmmoCaps(Actor player, float bonus)
	{
		for (Inventory inv = player.inv; inv != null; inv = inv.inv)
		{
			let ammo = Ammo(inv);
			if (!ammo) continue;
			let has_backpack = ammo.MaxAmount == ammo.BackpackMaxAmount;
			let defaults = GetDefaultByType(ammo.GetClass());
			ammo.BackpackMaxAmount = ceil(defaults.BackpackMaxAmount * bonus);
			ammo.MaxAmount = has_backpack ? ammo.BackpackMaxAmount : ceil(defaults.MaxAmount * bonus);
		}
	}

	override void OnActivate(TFLV_PerPlayerStats stats, TFLV_WeaponInfo info)
	{
		super.OnActivate(stats, info);
		UpdateAmmoCaps(stats.owner, AmmoBonus(self.level));
	}

	override void OnDeactivate(TFLV_PerPlayerStats stats, TFLV_WeaponInfo info)
	{
		super.OnDeactivate(stats, info);
		UpdateAmmoCaps(stats.owner, 1.0);
	}

	override void GetTooltipFields(Dictionary fields, uint level)
	{
		fields.insert("ammo-bonus", AsPercentIncrease(AmmoBonus(level)));
	}

	override bool IsSuitableForPlayer(TFLV_PerPlayerStats stats)
	{
		return true;
	}
}
