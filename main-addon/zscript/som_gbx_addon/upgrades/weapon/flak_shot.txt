class SOM_GBX_Upgrade_FlakShot: TFLV_Upgrade_BaseUpgrade
{
	override TFLV_Upgrade_UpgradePriority Priority()
	{
		return TFLV_Upgrade_PRI_EXPLOSIVE;
	}

	override void OnKill(PlayerPawn player, Actor shot, Actor target)
	{
		if (shot is "SOM_GBX_Upgrade_FlakShot_Grenade") return;
		let aux = SOM_GBX_Upgrade_FlakShot_Spawner(target.Spawn("SOM_GBX_Upgrade_FlakShot_Spawner", target.pos));
		aux.weaponspecial = Priority();
		aux.target = player;
		let count = som_gbx_upgr_flak_shot_base_flaks + level;
		aux.ReactionTime = count;
		aux.damage = ((target.SpawnHealth() + abs(target.health)) * (1.0 - 0.8 ** level)) / count + 4 * level;
		aux.damage *= som_gbx_upgr_flak_shot_dmg_mult;
		aux.blast_radius = target.radius * 2.5;
		aux.ttl = som_gbx_upgr_flak_shot_ttl;
	}

	override bool IsSuitableForWeapon(TFLV_WeaponInfo info)
	{
		return !info.IsMelee();
	}

	override void GetTooltipFields(Dictionary fields, uint level)
	{
		fields.insert("count", ""..(som_gbx_upgr_flak_shot_base_flaks + level));
		fields.insert("bonusdamage", ""..int(round((level * 4) * som_gbx_upgr_flak_shot_dmg_mult)));
		fields.insert("damage", AsPercent((1.0 - 0.8 ** level) * som_gbx_upgr_flak_shot_dmg_mult));
		fields.insert("ttl", AsSeconds(som_gbx_upgr_flak_shot_ttl * 35));
	}
}

class SOM_GBX_Upgrade_FlakShot_Spawner: Actor
{
	uint damage;
	uint blast_radius;
	uint ttl;

	States
	{
		Spawn:
			TNT1 A 7;
		SpawnMunition:
			TNT1 A 1 SpawnMunition();
			TNT1 A 0 A_CountDown();
			LOOP;
	}

	void SpawnMunition()
	{
		let aux = SOM_GBX_Upgrade_FlakShot_Grenade(A_SpawnProjectile(
			"SOM_GBX_Upgrade_FlakShot_Grenade", 32, 0, random(0, 360),
			CMF_AIMDIRECTION | CMF_ABSOLUTEANGLE));
		aux.weaponspecial = weaponspecial;
		aux.target = target;
		aux.damage = damage;
		aux.blast_radius = blast_radius;
		aux.ReactionTime = random(1, round(ttl * 5));
	}
}

class SOM_GBX_Upgrade_FlakShot_Grenade: Actor
{
	uint damage;
	uint blast_radius;
	property UpgradePriority: weaponspecial;

	Default
	{
		SOM_GBX_Upgrade_FlakShot_Grenade.UpgradePriority TFLV_Upgrade_PRI_EXPLOSIVE;
		Radius 12;
		Height 24;
		Speed 15;
		Scale 0.4;
		DamageType "Extreme";
		Projectile;
		+SEEKERMISSILE;
		+NODAMAGETHRUST;
		-NOGRAVITY;
		BounceType "Grenade";
		BounceFactor 1.0;
	}

	override int DoSpecialDamage(Actor target, int damage, Name damagetype)
	{
		if (target == self.target)
		{
			return 1;
		}
		return damage;
	}

	States
	{
		Spawn:
			SFLK DCBA 7 Bright A_CountDown();
			LOOP;
		Death:
			LFBX A 0 A_Explode(damage, blast_radius, XF_NOSPLASH, false, blast_radius);
			LFBX A 0 A_AlertMonsters();
			LFBX A 0 A_StartSound("bonsai/smallboom", CHAN_WEAPON, CHANF_OVERLAP, 1, 0.5);
			LFBX A 0 A_StartSound("bonsai/smallboom", CHAN_7, CHANF_OVERLAP, 0.1, 0.01);
			LFBX ABC 7 Bright;
			STOP;
	}
}
