class SOM_GBX_Upgrade_Shatterdrive_Main: TFLV_Upgrade_BaseUpgrade
{
	int qty;
	float ttl;
	int dmg;
	
	uint Level(TFLV_WeaponInfo info, string cls) const
	{
		for (uint i = 0; i < info.upgrades.upgrades.Size(); ++i)
		{
			if (info.upgrades.upgrades[i].GetClassName() == cls)
			{
				if (!info.upgrades.upgrades[i].enabled)
				{
					return (0);
				}
				
				return info.upgrades.upgrades[i].level;
			}
		}
		return 0;
	}

	override void Tick(Actor owner)
	{
		if (som_gbx_upgr_flak_shot_simple)
		{
			enabled = false;
		}
		
		let info = TFLV_PerPlayerStats.GetStatsFor(owner).GetInfoForCurrentWeapon();
		
		qty = som_gbx_upgr_flak_shot_flaks_base + (som_gbx_upgr_flak_shot_flaks_inc * Level(info, "SOM_GBX_Upgrade_Shatterdrive_Qty"));
		ttl = som_gbx_upgr_flak_shot_ttl_base + (som_gbx_upgr_flak_shot_ttl_inc * float(Level(info, "SOM_GBX_Upgrade_Shatterdrive_Ttl")));
		dmg = som_gbx_upgr_flak_shot_dmg_base + (som_gbx_upgr_flak_shot_dmg_inc * Level(info, "SOM_GBX_Upgrade_Shatterdrive_Dmg"));
	}
	
	override TFLV_Upgrade_UpgradePriority Priority()
	{
		return TFLV_Upgrade_PRI_EXPLOSIVE;
	}

	override void OnKill(PlayerPawn player, Actor shot, Actor target)
	{
		if (shot is "SOM_GBX_Upgrade_Shatterdrive_Grenade") return;
		let aux = SOM_GBX_Upgrade_Shatterdrive_Spawner(target.Spawn("SOM_GBX_Upgrade_Shatterdrive_Spawner", target.pos));
		aux.weaponspecial = Priority();
		aux.target = player;
		let count = qty;
		aux.ReactionTime = count;
		aux.damage = ((target.SpawnHealth() + abs(target.health)) * (1.0 - 0.8 ** dmg)) / count + 4 * dmg;
		aux.damage *= som_gbx_upgr_flak_shot_dmg_mult;
		aux.blast_radius = target.radius * 2.5;
		aux.ttl = ttl;
	}

	override bool IsSuitableForWeapon(TFLV_WeaponInfo info)
	{
		return som_gbx_upgr_flak_shot_enabled
			&& !info.IsMelee()
			&& !som_gbx_upgr_flak_shot_simple
			&& info.upgrades.Level("SOM_GBX_Upgrade_Shatterdrive_Main") == 0;
	}

	override void GetTooltipFields(Dictionary fields, uint level)
	{
		fields.insert("count", ""..(som_gbx_upgr_flak_shot_flaks_base));
		fields.insert("bonusdamage", ""..int(round((som_gbx_upgr_flak_shot_dmg_base * 4) * som_gbx_upgr_flak_shot_dmg_mult)));
		fields.insert("damage", AsPercent((1.0 - 0.8 ** som_gbx_upgr_flak_shot_dmg_base) * som_gbx_upgr_flak_shot_dmg_mult));
		fields.insert("ttl", AsSeconds(som_gbx_upgr_flak_shot_ttl_base * 35));
	}
}

class SOM_GBX_Upgrade_FlakShot: TFLV_Upgrade_BaseUpgrade
{
	override void Tick(Actor owner)
	{
		if (!som_gbx_upgr_flak_shot_simple)
		{
			enabled = false;
		}
	}
	
	override TFLV_Upgrade_UpgradePriority Priority()
	{
		return TFLV_Upgrade_PRI_EXPLOSIVE;
	}

	override void OnKill(PlayerPawn player, Actor shot, Actor target)
	{
		if (shot is "SOM_GBX_Upgrade_Shatterdrive_Grenade") return;
		let aux = SOM_GBX_Upgrade_Shatterdrive_Spawner(target.Spawn("SOM_GBX_Upgrade_Shatterdrive_Spawner", target.pos));
		aux.weaponspecial = Priority();
		aux.target = player;
		let count = som_gbx_upgr_flak_shot_flaks_base + (level - 1);	// -1 to make it the same as Skill Tree Version
		aux.ReactionTime = count;
		aux.damage = ((target.SpawnHealth() + abs(target.health)) * (1.0 - 0.8 ** level)) / count + 4 * level;
		aux.damage *= som_gbx_upgr_flak_shot_dmg_mult;
		aux.blast_radius = target.radius * 2.5;
		aux.ttl = som_gbx_upgr_flak_shot_ttl_base;
	}

	override bool IsSuitableForWeapon(TFLV_WeaponInfo info)
	{
		return som_gbx_upgr_flak_shot_enabled
			&& !info.IsMelee()
			&& som_gbx_upgr_flak_shot_simple;
	}

	override void GetTooltipFields(Dictionary fields, uint level)
	{
		fields.insert("count", ""..(som_gbx_upgr_flak_shot_flaks_base + (level - 1)));
		fields.insert("bonusdamage", ""..int((level * 4) * som_gbx_upgr_flak_shot_dmg_mult));
		fields.insert("damage", AsPercent((1.0 - 0.8 ** level) * som_gbx_upgr_flak_shot_dmg_mult));
		fields.insert("ttl", AsSeconds(som_gbx_upgr_flak_shot_ttl_base * 35));
	}
}

class SOM_GBX_Upgrade_Shatterdrive_Spawner: Actor
{
	uint damage;
	uint blast_radius;
	float ttl;

	States
	{
		Spawn:
			TNT1 A 7;
		SpawnMunition:
			TNT1 A 1 SpawnMunition();
			TNT1 A 0 A_CountDown();
			LOOP;
	}

	void SpawnMunition()
	{
		let aux = SOM_GBX_Upgrade_Shatterdrive_Grenade(A_SpawnProjectile(
			"SOM_GBX_Upgrade_Shatterdrive_Grenade", 32, 0, random(0, 360),
			CMF_AIMDIRECTION | CMF_ABSOLUTEANGLE));
		aux.weaponspecial = weaponspecial;
		aux.target = target;
		aux.damage = damage;
		aux.blast_radius = blast_radius;
		// ttl is in # of seconds, bomblets process 5x/sec, multiply ttl * 5
		aux.ReactionTime = random(1, round(ttl * 5.0));
	}
}

class SOM_GBX_Upgrade_Shatterdrive_Grenade: Actor
{
	uint damage;
	uint blast_radius;
	property UpgradePriority: weaponspecial;

	Default
	{
		SOM_GBX_Upgrade_Shatterdrive_Grenade.UpgradePriority TFLV_Upgrade_PRI_EXPLOSIVE;
		Radius 12;
		Height 24;
		Speed 15;
		Scale 0.4;
		DamageType "Extreme";
		Projectile;
		+SEEKERMISSILE;
		+NODAMAGETHRUST;
		-NOGRAVITY;
		BounceType "Grenade";
		BounceFactor 1.0;
	}

	override int DoSpecialDamage(Actor target, int damage, Name damagetype)
	{
		if (target == self.target)
		{
			return 1;
		}
		return damage;
	}

	States
	{
		Spawn:
			SFLK DCBA 7 Bright A_CountDown();
			LOOP;
		Death:
			LFBX A 0 A_Explode(damage, blast_radius, XF_NOSPLASH, false, blast_radius);
			LFBX A 0 A_AlertMonsters();
			LFBX A 0 A_StartSound("bonsai/smallboom", CHAN_WEAPON, CHANF_OVERLAP, 1, 0.5);
			LFBX A 0 A_StartSound("bonsai/smallboom", CHAN_7, CHANF_OVERLAP, 0.1, 0.01);
			LFBX ABC 7 Bright;
			STOP;
	}
}

class SOM_GBX_Upgrade_Shatterdrive_Qty: TFLV_Upgrade_BaseUpgrade
{
	override bool IsSuitableForWeapon(TFLV_WeaponInfo info)
	{
		return som_gbx_upgr_flak_shot_enabled
			&& !info.IsMelee()
			&& !som_gbx_upgr_flak_shot_simple
			&& info.upgrades.Level("SOM_GBX_Upgrade_Shatterdrive_Main") > 0;
	}
	
	override void GetTooltipFields(Dictionary fields, uint level)
	{
		int qty = som_gbx_upgr_flak_shot_flaks_inc * level;
		
		fields.insert("count", ""..(qty));
	}
}

class SOM_GBX_Upgrade_Shatterdrive_Dmg: TFLV_Upgrade_BaseUpgrade
{
	override bool IsSuitableForWeapon(TFLV_WeaponInfo info)
	{
		return som_gbx_upgr_flak_shot_enabled
			&& !info.IsMelee()
			&& !som_gbx_upgr_flak_shot_simple
			&& info.upgrades.Level("SOM_GBX_Upgrade_Shatterdrive_Main") > 0;
	}
	
	override void GetTooltipFields(Dictionary fields, uint level)
	{
		int dmg = som_gbx_upgr_flak_shot_dmg_inc * level;
		
		fields.insert("bonusdamage", ""..int(round((dmg * 4) * som_gbx_upgr_flak_shot_dmg_mult)));
		fields.insert("damage", AsPercent((1.0 - 0.8 ** dmg) * som_gbx_upgr_flak_shot_dmg_mult));
	}
}

class SOM_GBX_Upgrade_Shatterdrive_Ttl: TFLV_Upgrade_BaseUpgrade
{
	override bool IsSuitableForWeapon(TFLV_WeaponInfo info)
	{
		return som_gbx_upgr_flak_shot_enabled
			&& !info.IsMelee()
			&& !som_gbx_upgr_flak_shot_simple
			&& info.upgrades.Level("SOM_GBX_Upgrade_Shatterdrive_Main") > 0;
	}
	
	override void GetTooltipFields(Dictionary fields, uint level)
	{
		float ttl = som_gbx_upgr_flak_shot_ttl_inc * float(level);
		
		fields.insert("ttl", AsSeconds(ttl * 35));
	}
}
