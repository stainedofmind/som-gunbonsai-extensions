class SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo : object play
{
	Ammo ammo_class;
	String ammo_id;
	String ammo_name;
	uint ammo_max_amt;
	uint ammo_curr_amt;
	uint res_max_amt;
	uint res_curr_amt;
	
	void init(Actor owner, string _id, uint level)
	{
		ammo_id = _id;
		
		update_data(owner, level);
		
		class<Actor> cls = _id;
		
		ammo_name = Actor(GetDefaultByType(cls)).GetTag();
		
		if (ammo_name == "")
		{
			ammo_name = _id;
		}
	}
	
	void update_data(Actor owner, uint level)
	{
		Let p = owner;
		
		if (!ammo_class)
		{
			ammo_class = Ammo(p.FindInventory(ammo_id));
			
			if (!ammo_class) { Return; }
		}
		
		int _max = ammo_class.MaxAmount;
		int _bp = ammo_class.BackpackMaxAmount;
		
		ammo_curr_amt = p.CountInv(ammo_id);
		
		if (_max > _bp)
		{
			ammo_max_amt = ammo_class.MaxAmount;
		}
		else
		{
			ammo_max_amt = (p.CountInv("Backpack")) ? _bp : _max;
		}
		
		res_max_amt = round(ammo_max_amt * (som_gbx_upgr_ordinance_array_inc * level));
	}
	
	int modify_reserve(int amt)
	{
		int new = res_curr_amt + amt;
		
		if (new > int(res_max_amt))
		{
			amt = res_max_amt - res_curr_amt;
		}
		
		if (new < 0)
		{
			amt = -res_curr_amt;
		}
		
		res_curr_amt += amt;
		
		return (abs(amt));
	}
	
	bool reserve_is_full()
	{
		return (res_curr_amt >= res_max_amt);
	}
	
	bool reserve_is_empty()
	{
		return (res_curr_amt <= 0);
	}
}

class SOM_GBX_Upgrade_OrdinanceArray : TFLV_Upgrade_BaseUpgrade
{
	const HUD_ID = "upgr-ordinance-array";
	
	array<SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo> ammo_info;
	
	som_gbx_hud_widget init_hud()
	{
		Let gc = som_gbx_global_container.Get();
		
		return (gc.g.hud.new_widget(HUD_ID, "som_gbx_upgr_ordinance_array_hud_"));
	}

	SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo GetInfoFor(string _id) const
	{
		for (int i = 0; i < ammo_info.size(); ++i)
		{
			if (ammo_info[i].ammo_id == _id)
			{
				return ammo_info[i];
			}
		}
		
		return null;
	}
	
	bool is_valid_ammo(Ammo ammo)
	{
		bool valid = (GetDefaultByType(ammo.GetClass()).BackpackAmount != 0) || (GetDefaultByType(ammo.GetClass()).FindState("Spawn").Sprite != 0);
		
		return (valid);
	}
	
	void find_new_ammo(Actor owner)
	{
		Let p = owner;
		
		for (Inventory inv = p.inv; inv != null; inv = inv.inv)
		{
			let ammo = Ammo(inv);
			
			if (!ammo) continue;
			
			string _id = ammo.GetClassName();
			
			Let _info = GetInfoFor(_id);
			
			if (!_info)
			{
				if (!is_valid_ammo(ammo))
				{
					continue;
				}
			
				_info = new("SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo");
				_info.init(owner, _id, level);
				ammo_info.push(_info);
			}
		}
	}
	
	override bool IsSuitableForPlayer(TFLV_PerPlayerStats stats)
	{
		return (som_gbx_upgr_ordinance_array_enabled);
	}
	
	override void Tick(Actor owner)
	{
		// HUD Stuff
		Let gc = som_gbx_global_container.Get();
		
		som_gbx_hud_widget wid = gc.g.hud.get_widget(HUD_ID);
		
		if (!wid)
		{
			wid = init_hud();
		}
			
		if (enabled)
		{
			Let p = owner;
			
			find_new_ammo(owner);
			
			SOM_GBX_Upgrade_OrdinanceArray_AmmoInfo info;
			int ammo = 0;
			int pivot = 0;
			int amt = 0;
			
			for (int i = 0; i < ammo_info.Size(); i++)
			{
				info = ammo_info[i];
				
				info.update_data(owner, level);
				
				ammo = p.CountInv(info.ammo_id);
				
				pivot = floor(info.ammo_max_amt * som_gbx_upgr_ordinance_array_pivot);
				
				if ((ammo > pivot) && (!info.reserve_is_full()))
				{
					amt = ammo - pivot;
					
					amt = info.modify_reserve(amt);
					
					p.TakeInventory(info.ammo_id, amt);
				}

				if ((ammo < pivot) && (!info.reserve_is_empty()))
				{
					amt = pivot - ammo;
					
					if (amt > 0)
					{
						amt = info.modify_reserve(-amt);
						p.GiveInventory(info.ammo_id, amt);
					}
				}
			}
			
			// Build HUD
			wid.draw = som_gbx_upgr_ordinance_array_hud_enabled;
			
			wid.lines.Clear();
			
			wid.lines.push("Ordinance Array");
			
			bool no_ord = true;

			let weap = owner.player.readyweapon;
			
			if (weap)
			{
				Let ammo = weap.ammotype1;
				
				if (ammo)
				{
					info = GetInfoFor(ammo.GetClassName());
					
					if (info)
					{
						no_ord = false;
						wid.lines.push(String.Format("%s:", info.ammo_name));
						wid.lines.push(String.Format("%d/%d", info.res_curr_amt, info.res_max_amt));
					}
				}

				ammo = weap.ammotype2;
				
				if (ammo)
				{
					info = GetInfoFor(ammo.GetClassName());
					
					if (info)
					{
						no_ord = false;
						wid.lines.push(String.Format("%s:", info.ammo_name));
						wid.lines.push(String.Format("%d/%d", info.res_curr_amt, info.res_max_amt));
					}
				}
			}
			
			if (no_ord)
			{
				wid.lines.push("<NULL_AMMO>");
			}
		}
	}
	
	override void OnActivate(TFLV_PerPlayerStats stats, TFLV_WeaponInfo info)
	{
		Let gc = som_gbx_global_container.Get();
		
		Let wid = gc.g.hud.get_widget(HUD_ID);
			
		if (!wid)
		{
			wid = init_hud();
		}
		
		wid.draw = som_gbx_upgr_ordinance_array_hud_enabled;
	}

	override void OnDeactivate(TFLV_PerPlayerStats stats, TFLV_WeaponInfo info)
	{
		Let gc = som_gbx_global_container.Get();
		
		Let wid = gc.g.hud.get_widget(HUD_ID);
		
		if (!wid)
		{
			wid = init_hud();
		}
		
		wid.draw = false;
	}

	override void GetTooltipFields(Dictionary fields, uint level)
	{
		fields.insert("ammo-pool", AsPercent((som_gbx_upgr_ordinance_array_inc * level)));
	}
}
